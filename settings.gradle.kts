// SPDX-License-Identifier: Apache-2.0
import org.hiero.gradle.environment.EnvAccess

plugins {
    id("org.hiero.gradle.build") version "0.7.7"
    id("com.hedera.pbj.pbj-compiler") version "0.15.2" apply false
}

javaModules {
    // The Hedera API module
    directory("hapi") { group = "com.hedera.hashgraph" }

    // The Hedera platform modules
    directory("platform-sdk") { group = "com.hedera.hashgraph" }

    // The Hedera services modules
    directory("hedera-node") {
        group = "com.hedera.hashgraph"

        // Configure 'artifact' for projects where folder does not correspond to artifact name
        module("hapi-fees") { artifact = "app-hapi-fees" }
        module("hapi-utils") { artifact = "app-hapi-utils" }
        module("hedera-addressbook-service") { artifact = "app-service-addressbook" }
        module("hedera-addressbook-service-impl") { artifact = "app-service-addressbook-impl" }
        module("hedera-app") { artifact = "app" }
        module("hedera-app-spi") { artifact = "app-spi" }
        module("hedera-config") { artifact = "config" }
        module("hedera-consensus-service") { artifact = "app-service-consensus" }
        module("hedera-consensus-service-impl") { artifact = "app-service-consensus-impl" }
        module("hedera-file-service") { artifact = "app-service-file" }
        module("hedera-file-service-impl") { artifact = "app-service-file-impl" }
        module("hedera-network-admin-service") { artifact = "app-service-network-admin" }
        module("hedera-network-admin-service-impl") { artifact = "app-service-network-admin-impl" }
        module("hedera-schedule-service") { artifact = "app-service-schedule" }
        module("hedera-schedule-service-impl") { artifact = "app-service-schedule-impl" }
        module("hedera-smart-contract-service") { artifact = "app-service-contract" }
        module("hedera-smart-contract-service-impl") { artifact = "app-service-contract-impl" }
        module("hedera-token-service") { artifact = "app-service-token" }
        module("hedera-token-service-impl") { artifact = "app-service-token-impl" }
        module("hedera-util-service") { artifact = "app-service-util" }
        module("hedera-util-service-impl") { artifact = "app-service-util-impl" }
        module("hedera-roster-service") { artifact = "app-service-roster" }
        module("hedera-roster-service-impl") { artifact = "app-service-roster-impl" }
        module("hedera-entity-id-service") { artifact = "app-service-entity-id" }
        module("hedera-entity-id-service-impl") { artifact = "app-service-entity-id-impl" }
    }

    // Platform-base demo applications
    directory("example-apps") { group = "com.hedera.hashgraph" }

    directory("hiero-observability") { group = "com.hedera.hashgraph" }

    module("hedera-state-validator") { group = "com.hedera.hashgraph" }
}

// Flaky test handling
@Suppress("UnstableApiUsage")
gradle.lifecycle.afterProject {
    tasks.withType<Test>().configureEach {
        reports.junitXml.mergeReruns = true

        // CI: configure rerun to accept and track flakiness
        develocity.testRetry {
            maxRetries = if (EnvAccess.isCiServer(providers)) 2 else 0
            maxFailures = 10
            failOnPassedAfterRetry = false
        }
        // Write a marker when tests actually execute (not on cache restore).
        val markerFile = layout.buildDirectory.file("test-executed/${name}.marker").get().asFile
        doLast {
            markerFile.parentFile.mkdirs()
            markerFile.writeText(java.time.Instant.now().toString())
        }

        // Local build: add '-PrunUntilFailure=<maxRetries>' option to check that a test is (likely)
        // not flaky
        val runUntilFailure = providers.gradleProperty("runUntilFailure").map { it.toInt() }
        if (runUntilFailure.isPresent) {
            // no up-to-date or caching in 'runUntilFailure' mode
            doNotTrackState("Run until failure mode")
            // re-execute task action (executeTests()) until failure or max rerun reached
            doLast {
                for (rerunIndex in 1..runUntilFailure.get()) {
                    logger.lifecycle("Test Rerun $rerunIndex/${runUntilFailure.get()}")
                    executeTests()
                }
            }
        }
    }
}
